/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'dconnolly' at '10/15/19 10:22 AM' with Gradle 2.13
 *
 * cribbed from https://stackoverflow.com/a/44803796
 */
plugins {
    id 'groovy'
    id 'idea'
}

repositories { mavenCentral() }

dependencies {
    compile 'org.codehaus.groovy:groovy:2.5.8'
    compile 'org.codehaus.groovy:groovy-json:2.5.8'
    compile 'org.codehaus.groovy:groovy-sql:2.5.8'
    compile 'com.imsweb:naaccr-xml:6.6'
    compile 'com.imsweb:layout:2.0'
    compile 'tech.tablesaw:tablesaw-core:0.37.2'
    // https://mvnrepository.com/artifact/com.h2database/h2/1.4.200
    compile 'com.h2database:h2:1.4.200'

    // https://mvnrepository.com/artifact/com.offbytwo/docopt
    compile group: 'com.offbytwo', name: 'docopt', version: '0.6.0.20150202'

    // https://mvnrepository.com/artifact/org.slf4j/slf4j-simple
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.30'
    // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'

    // i2b2 JDBC drivers
    // https://github.com/i2b2/i2b2-core-server/tree/master/edu.harvard.i2b2.server-common/lib/jdbc
    // mssql-jdbc-7.4.1.jre8.jar, ojdbc8.jar, postgresql-42.2.8.jar
    runtime 'com.oracle.ojdbc:ojdbc8:19.3.0.0'
    runtime 'org.postgresql:postgresql:42.2.8.jre7'
    runtime 'com.microsoft.sqlserver:mssql-jdbc:8.1.0.jre13-preview'

    testCompile 'junit:junit:4.12'
    testCompile 'org.codehaus.groovy:groovy-test:3.0.1'
}

class UsageDoc extends DefaultTask {
    @InputFile
    File readme = project.file('README.md')

    @OutputFile
    File doc = project.file('src/main/resources/usage.txt')

    @TaskAction
    void perform() {
        final usage = readme.text.split('```\n').find { it.startsWith('Usage:') }
        doc.text = usage
    }
}

task buildUsageDoc(type: UsageDoc) {
    group = 'documentation'
}
processResources.dependsOn buildUsageDoc

task fatjar(type: Jar) {
    group = 'build'

    manifest {
        attributes 'Main-Class': 'TumorFile'
    }
    from {
        configurations
            .runtime
            .collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar

    // avoid: Error: Could not find or load main class
    // https://stackoverflow.com/a/51456080
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}

fatjar.dependsOn build

Properties db1() {
    Properties ps = new Properties()
    ps.putAll(["db.url"              : "jdbc:h2:file:${System.getProperty('user.dir')}/DB1;create=true".toString(),
               "db.driver"           : 'org.h2.Driver',
               "db.username"         : 'SA',
               "db.password"         : '',
               "naaccr.flat-file"    : 'naaccr_xml_samples/naaccr-xml-sample-v180-incidence-100.txt',
               "naaccr.records-table": "NAACCR_RECORDS"])
    ps
}

task localDBRecordsTest(type: Exec) {
    description = 'Integration tests: build NAACCR_ONTOLOGY in a local disk h2 DB.'
    group = 'verification'

    db1().store(new File('db.properties').newWriter(), null)

    commandLine 'java', '-jar', 'build/libs/naaccr-tumor-data.jar', 'load-records'
}
localDBRecordsTest.dependsOn fatjar

task localDBStatsTest(type: Exec) {
    description = 'Integration tests: stats on 100 records of test data with local disk h2 DB.'
    group = 'verification'

    db1().store(new File('db.properties').newWriter(), null)

    commandLine 'java', '-jar', 'build/libs/naaccr-tumor-data.jar', 'summary', '--no-file'
}
localDBStatsTest.dependsOn localDBRecordsTest

task localDBTumorsTest(type: Exec) {
    description = 'Integration tests: visits from 100 records of test data with local disk h2 DB.'
    group = 'verification'

    db1().store(new File('db.properties').newWriter(), null)

    commandLine 'java', '-jar', 'build/libs/naaccr-tumor-data.jar', 'tumors'
}
localDBTumorsTest.dependsOn fatjar

task localDBFactsTest(type: Exec) {
    description = 'Integration tests: facts from 100 records of test data with local disk h2 DB.'
    group = 'verification'

    db1().store(new File('db.properties').newWriter(), null)

    commandLine 'java', '-jar', 'build/libs/naaccr-tumor-data.jar', 'facts'
}
localDBFactsTest.dependsOn fatjar

task localDBOntologyTest(type: Exec) {
    description = 'Integration tests: build NAACCR_ONTOLOGY in a local disk h2 DB.'
    group = 'verification'

    db1().store(new File('db.properties').newWriter(), null)

    commandLine 'java', '-jar', 'build/libs/naaccr-tumor-data.jar', 'ontology'
}
localDBOntologyTest.dependsOn fatjar

task localDBImportTask(type: Exec) {
    description = 'Integration tests: import 2017 ontology into local disk h2 DB.'
    group = 'verification'

    db1().store(new File('db.properties').newWriter(), null)

    commandLine 'java', '-jar', 'build/libs/naaccr-tumor-data.jar', 'import', 'ONT_2017', 'heron_load/shrine_ont.naaccr_ontology.csv', 'heron_load/shrine_ont.naaccr_ontology-metadata.json'
}
localDBImportTask.dependsOn fatjar

task integrationTest(type: Task) {
    description = 'Run all integration tests; for example, in preparation for a release'
    group = 'verification'
}
integrationTest.dependsOn localDBTumorsTest
integrationTest.dependsOn localDBFactsTest
integrationTest.dependsOn localDBStatsTest
integrationTest.dependsOn localDBOntologyTest
integrationTest.dependsOn localDBRecordsTest

