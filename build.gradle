/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'dconnolly' at '10/15/19 10:22 AM' with Gradle 2.13
 *
 * cribbed from https://stackoverflow.com/a/44803796
 */
plugins {
    id 'groovy'
    id 'idea'
}

repositories { mavenCentral() }

dependencies {
    compile 'org.codehaus.groovy:groovy:2.5.8'
    compile 'org.codehaus.groovy:groovy-json:2.5.8'
    compile 'org.codehaus.groovy:groovy-sql:2.5.8'
    compile 'com.imsweb:naaccr-xml:6.6'
    compile 'com.imsweb:layout:2.0'
    compile 'tech.tablesaw:tablesaw-core:0.37.2'
    // https://mvnrepository.com/artifact/com.h2database/h2/1.4.200
    compile 'com.h2database:h2:1.4.200'

    // i2b2 JDBC drivers
    // https://github.com/i2b2/i2b2-core-server/tree/master/edu.harvard.i2b2.server-common/lib/jdbc
    // mssql-jdbc-7.4.1.jre8.jar, ojdbc8.jar, postgresql-42.2.8.jar
    runtime 'com.oracle.ojdbc:ojdbc8:19.3.0.0'
    runtime 'org.postgresql:postgresql:42.2.8.jre7'
    runtime 'com.microsoft.sqlserver:mssql-jdbc:8.1.0.jre13-preview'

    testCompile 'junit:junit:4.12'
    testCompile 'org.codehaus.groovy:groovy-test:3.0.1'
}


def db1() {
    [DB1_URL: "jdbc:h2:file:${System.getProperty('user.dir')}/DB1;create=true",
     DB1_DRIVER: 'org.h2.Driver',
     DB1_USER: 'SA', DB1_PASSWORD: '']
}

task localDBStatsTest(type: JavaExec) {
    description = 'Integration tests: stats on 100 records of test data with local disk h2 DB.'
    group = 'verification'

    main = "TumorFile"
    environment = db1()

    args '--account', 'DB1', '--summary', '--flat-file', 'naaccr_xml_samples/naaccr-xml-sample-v180-incidence-100.txt'
    classpath = sourceSets.main.runtimeClasspath
    // TODO: shouldRunAfter test
}

task localDBVistsTest(type: JavaExec) {
    description = 'Integration tests: visits from 100 records of test data with local disk h2 DB.'
    group = 'verification'

    main = "TumorFile"
    environment = db1()

    args '--account', 'DB1', '--visits', '--flat-file', 'naaccr_xml_samples/naaccr-xml-sample-v180-incidence-100.txt'
    classpath = sourceSets.main.runtimeClasspath
    // TODO: shouldRunAfter test
}

task localDBFactsTest(type: JavaExec) {
    description = 'Integration tests: facts from 100 records of test data with local disk h2 DB.'
    group = 'verification'

    main = "TumorFile"
    environment = db1()

    args '--account', 'DB1', '--facts', '--flat-file', 'naaccr_xml_samples/naaccr-xml-sample-v180-incidence-100.txt'
    classpath = sourceSets.main.runtimeClasspath
    // TODO: shouldRunAfter test
}

// TODO: check.dependsOn integrationTest


task fatjar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'Loader'
    }
    from {
        configurations
            .runtime
            .collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar

    // avoid: Error: Could not find or load main class
    // https://stackoverflow.com/a/51456080
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'

}
